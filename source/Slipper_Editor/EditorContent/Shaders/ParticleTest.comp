#version 450

struct Particle {
	vec2 position;
	vec2 velocity;
    vec4 color;
};

layout (binding = 0) uniform ParameterUBO {
    float deltaTime;
} ubo;

layout(std140, binding = 1) readonly buffer ParticleSSBOIn {
   Particle particles[ ];
} pIn;

layout(std140, binding = 2) buffer ParticleSSBOOut {
   Particle particles[ ];
} pOut;

layout (local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

void main() 
{
    uint index = gl_GlobalInvocationID.x;  

    Particle particleIn = pIn.particles[index];

    pOut.particles[index].position = particleIn.position + particleIn.velocity.xy * ubo.deltaTime;
    pOut.particles[index].velocity = particleIn.velocity;

    // Flip movement at window border
    if ((pOut.particles[index].position.x <= -1.0) || (pOut.particles[index].position.x >= 1.0)) {
        pOut.particles[index].velocity.x = -pOut.particles[index].velocity.x;
    }
    if ((pOut.particles[index].position.y <= -1.0) || (pOut.particles[index].position.y >= 1.0)) {
        pOut.particles[index].velocity.y = -pOut.particles[index].velocity.y;
    }

}